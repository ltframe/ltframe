#!/usr/bin/perl -w

# Copyright (C) 2006, 2007, 2008, 2009 Apple Inc.  All rights reserved.
# Copyright (C) 2006 Mark Rowe <opendarwin.org@bdash.net.nz>.  All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
#
# 1.  Redistributions of source code must retain the above copyright
#     notice, this list of conditions and the following disclaimer. 
# 2.  Redistributions in binary form must reproduce the above copyright
#     notice, this list of conditions and the following disclaimer in the
#     documentation and/or other materials provided with the distribution. 
# 3.  Neither the name of Apple Inc. ("Apple") nor the names of
#     its contributors may be used to endorse or promote products derived
#     from this software without specific prior written permission. 
#
# THIS SOFTWARE IS PROVIDED BY APPLE AND ITS CONTRIBUTORS "AS IS" AND ANY
# EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL APPLE OR ITS CONTRIBUTORS BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
# ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
# THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

# Creates the launcher WebKit.app with bundled frameworks.

use strict;

use FindBin;
use lib "$FindBin::Bin/../Scripts";
use Cwd 'realpath';
use Getopt::Long;
use webkitdirs;
use VCSUtils;

my $branch;
my $leopardProductDir;
my $productDir;
my $revision;
my $tigerProductDir;
my $snowLeopardProductDir;
my $lionProductDir;

GetOptions('branch=s' => \$branch,
           'revision=i' => \$revision,
           'build-dir=s' => \$productDir,
           'build-dir-10-4=s' => \$tigerProductDir,
           'build-dir-10-5=s' => \$leopardProductDir,
           'build-dir-10-6=s', => \$snowLeopardProductDir,
           'build-dir-10-7=s', => \$lionProductDir,
    );

$branch = currentBranch() unless defined($branch);
$revision = currentSVNRevision() unless defined($revision);

setBaseProductDir($productDir);

my @xcodeBuildArguments = XcodeOptions();
unshift @xcodeBuildArguments, "SYMROOT=$productDir";
unshift @xcodeBuildArguments, "OBJROOT=$productDir";

my $nightlyLauncherTemplatePath = "$FindBin::Bin/../WebKitLauncher";
my $nightlyLauncherStagingPath = $productDir . "/WebKit.app";

my $configuration = $ENV{'RC_JASPER'} eq 'YES' ? '' : configuration();

sub buildNightlyLauncher
{
    chdir($nightlyLauncherTemplatePath);
    system("xcodebuild", "clean", "-alltargets", @xcodeBuildArguments, @ARGV) == 0 or die "Failed cleaning WebKitLauncher project";
    system("xcodebuild", @xcodeBuildArguments, @ARGV) == 0 or die "Failed building WebKitLauncher project";
    chdirWebKit();
}

sub currentBranch
{
    my $sourceDir = sourceDir();
    my ($url, $branch);
    if (isSVNDirectory($sourceDir)) {
        my $svnInfo = `LC_ALL=C svn info $sourceDir | grep URL:`;
        ($url) = ($svnInfo =~ m/URL: (.+)/g);
    } elsif (isGitDirectory($sourceDir)) {
        my $gitLog = `cd $sourceDir && LC_ALL=C git log --grep='git-svn-id: ' -n 1 | grep git-svn-id:`;
        ($url) = ($gitLog =~ m/ +git-svn-id: (.+)@\d+ /g);
    }
    ($branch) = ($url =~ m/\/webkit\/(trunk|branches\/[^\/]+)/);
    die "Unable to determine current SVN branch in $sourceDir" unless (defined $branch);
    $branch =~ s/^branches\///;
    return $branch;
}

sub copyNightlyLauncher
{
    my $infoPlist = "$nightlyLauncherStagingPath/Contents/Info.plist";
    my $versionFile = "$nightlyLauncherStagingPath/Contents/Resources/VERSION";
    my $branchFile = "$nightlyLauncherStagingPath/Contents/Resources/BRANCH";
    my $data;

    open(IN, $infoPlist) or die "Couldn't open Info.plist in built application for reading";
    {
        undef $/;
        $data = <IN>;
    }
    close(IN);
    open(OUT, ">$infoPlist") or die "Couldn't open Info.plist in built application for writing";
    $data =~ s/VERSION/$revision/g;
    print OUT $data;
    close(OUT);

    open(OUT, ">$versionFile") or die "Couldn't open VERSION in built application for writing";
    print OUT "$revision\n";
    close(OUT);

    open(OUT, ">$branchFile") or die "Couldn't open BRANCH in built application for writing";
    print OUT "$branch\n";
    close(OUT);

    my @systemVersions;
    push @systemVersions, "10.5" if defined $leopardProductDir;
    push @systemVersions, "10.6" if defined $snowLeopardProductDir;
    push @systemVersions, "10.7" if defined $lionProductDir;

    for my $systemVersion (@systemVersions) {
        my $frameworksPath = "$nightlyLauncherStagingPath/Contents/Frameworks/$systemVersion";
        mkdir($frameworksPath);
        symlink("../Sparkle.framework", "$frameworksPath/Sparkle.framework");
    }

    my @frameworks = ("JavaScriptCore", "WebCore", "WebKit", "WebKit2");
    for my $framework (@frameworks) {
        for my $systemVersion (@systemVersions) { 
            if ($systemVersion eq "10.5") {
                next if $framework eq "WebKit2";

                my $leopardFramework = "$leopardProductDir/$configuration/$framework.framework";
                system("ditto", $leopardFramework, "$nightlyLauncherStagingPath/Contents/Frameworks/10.5/$framework.framework") == 0 or die "Failed copying $leopardFramework into $nightlyLauncherStagingPath";
            } elsif ($systemVersion eq "10.6") {
                my $snowLeopardFramework = "$snowLeopardProductDir/$configuration/$framework.framework";
                system("ditto", $snowLeopardFramework, "$nightlyLauncherStagingPath/Contents/Frameworks/10.6/$framework.framework") == 0 or die "Failed copying $snowLeopardFramework into $nightlyLauncherStagingPath";
            } elsif ($systemVersion eq "10.7") {
                my $lionFramework = "$lionProductDir/$configuration/$framework.framework";
                system("ditto", $lionFramework, "$nightlyLauncherStagingPath/Contents/Frameworks/10.7/$framework.framework") == 0 or die "Failed copying $lionFramework into $nightlyLauncherStagingPath";
            }
        }
    }

    for my $systemVersion (@systemVersions) {
        my $infoPlistWithoutExtension = "$nightlyLauncherStagingPath/Contents/Frameworks/$systemVersion/WebKit.framework/Resources/Info";
        system("defaults", "write", $infoPlistWithoutExtension, "CFBundleShortVersionString", "r$revision");
        system("chmod", "go=r", "$infoPlistWithoutExtension.plist");
    }
}

chdirWebKit();
buildNightlyLauncher();
copyNightlyLauncher();
